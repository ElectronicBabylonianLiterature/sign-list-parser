Asl {
    SignList = (Sign | NoSign)+

    NoSign = "@nosign" signName TopLevelField* "@end sign"

    Sign = "@sign" signName TopLevelField* "@end sign"
    signName = simple | enclosed
    enclosed  = "|" compound "|"
    compound = compoundPart (compoundOperator compoundPart)*
    compoundPart = group | repeated | simple
    compoundOperator = "." | "+" | "@" | "&" | "%" | "×"
    group = "(" compound ")"
    repeated = digit+ "x" simple
    simple = ~digit (upper | subscript | digit)+ modifier? variant?
    modifier = "@" (lower | digit+)

    TopLevelField = Form
                  | Field

    Field = List
          | Unicode
          | ScopedField
          | FreeTextField<"@lit">
          | Value

    ScopedField = FreeTextField<"@note">
                | FreeTextField<"@inote">
                | FreeTextField<"@uname">
                | FreeTextField<"@uphase">
                | FreeTextField<"@inst">
                | FreeTextField<"@pname">
                | Comment

    Comment = "#" freetext

    List = "@list" alnum+

    Unicode = "@ucode" NonemptyListOf<codepoint, ".">
    codepoint = "x" hexDigit+
    
    FreeTextField<tag> = tag freetext
    freetext = (~"\n" any)+

    Value = ("@v?" | "@v-"| "@v") language? unknown? (slashedReading | reading)?  ("[" Instance "]")? ScopedField*
    Instance = (NoBrackets | Brackets)+
    Brackets = "[" NoBrackets "]"
    NoBrackets = (~("[" | "]") any)+
    language = "%" lower+
    slashedReading = "/" reading "/"
    reading = (digit+ ("/" digit+)*)? "("? (lower | subscript | digit | "-" | "ʾ")+ modifier* ")"? modifier* variant? "⁻"?
    unknown = "[...]" | "..." | "?"

    Form = "@form" variant signName Field* "@end form"?
    
    variant = "~" (lower | digit)+
    subscript =  "ₓ" | "₀".."₉"
    upper += "Š" | "Ṣ" | "Ṭ" | "Ś" | "Ḫ" | "Ŋ"
    lower += "š" | "ṣ" | "ṭ" | "ś" | "ḫ" | "ŋ"
 } 