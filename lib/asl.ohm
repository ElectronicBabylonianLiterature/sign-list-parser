Asl {
    SignList = (Sign | NoSign)+

    NoSign = "@nosign" name TopLevelField* endSign

    Sign = "@sign" name TopLevelField* endSign
    name = (~space any)+
    endSign = endTag<"sign">

    endTag<tag> = "@end" space+ tag
    
    TopLevelField = Form
                  | Field

    Field = FreeTextField<"@list">
          | Unicode
          | ScopedField
          | FreeTextField<"@lit">
          | Value
          | FreeTextField<"@uname">
          | FreeTextField<"@uphase">
          | FreeTextField<"@inst">
          | FreeTextField<"@pname">

    ScopedField = FreeTextField<"@note">
                | FreeTextField<"@inote">
                | Comment

    Comment = "#" freetext

    Unicode = "@ucode" NonemptyListOf<codepoint, ".">
    codepoint = "x" hexDigit+
    
    FreeTextField<tag> = tag freetext
    freetext = (~"\n" any)+

    Value = ("@v?" | "@v-"| "@v") language? name?  ("[" Instance "]")? ScopedField*
    language = "%" lower+
    Instance = (NoBrackets | Brackets)+
    Brackets = "[" NoBrackets "]"
    NoBrackets = (~("[" | "]") any)+


    Form = "@form" variant name Field* endTag<"form">?
    
    variant = "~" (lower | digit)+
    subscript =  "ₓ" | "₀".."₉"
    upper += "Š" | "Ṣ" | "Ṭ" | "Ś" | "Ḫ" | "Ŋ"
    lower += "š" | "ṣ" | "ṭ" | "ś" | "ḫ" | "ŋ"
 } 