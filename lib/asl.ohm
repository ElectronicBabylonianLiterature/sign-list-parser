Asl {
    SignList = (Sign | NoSign)+

    NoSign = "@nosign" signName TopLevelField* endSign

    Sign = "@sign" signName TopLevelField* endSign
    signName = simple | enclosed
    enclosed  = "|" compound "|"
    compound = compoundPart (compoundOperator compoundPart)*
    compoundPart = group | repeated | simple
    compoundOperator = "." | "+" | "@" | "&" | "%" | "×"
    group = "(" compound ")"
    repeated = digit+ ("x" | "×") (simple | group)
    simple = ~digit (upper | subscript | digit)+ modifier* variant?
    modifier = "@" (lower | digit+)
    endSign = "@end" space "sign"

    TopLevelField = Form
                  | Field

    Field = FreeTextField<"@list">
          | Unicode
          | ScopedField
          | FreeTextField<"@lit">
          | Value
          | FreeTextField<"@uname">
          | FreeTextField<"@uphase">
          | FreeTextField<"@inst">
          | FreeTextField<"@pname">

    ScopedField = FreeTextField<"@note">
                | FreeTextField<"@inote">
                | Comment

    Comment = "#" freetext

    Unicode = "@ucode" NonemptyListOf<codepoint, ".">
    codepoint = "x" hexDigit+
    
    FreeTextField<tag> = tag freetext
    freetext = (~"\n" any)+

    Value = ("@v?" | "@v-"| "@v") language? reading?  ("[" Instance "]")? ScopedField*
    language = "%" lower+
    unknown = "Attinger" | "[...]" | "..." | "?" 
    slashedReading = "/" reading "/"
    reading = (~space any)+
    Instance = (NoBrackets | Brackets)+
    Brackets = "[" NoBrackets "]"
    NoBrackets = (~("[" | "]") any)+


    Form = "@form" variant signName Field* "@end form"?
    
    variant = "~" (lower | digit)+
    subscript =  "ₓ" | "₀".."₉"
    upper += "Š" | "Ṣ" | "Ṭ" | "Ś" | "Ḫ" | "Ŋ"
    lower += "š" | "ṣ" | "ṭ" | "ś" | "ḫ" | "ŋ"
 } 