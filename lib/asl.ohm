Asl {
    Sign = "@sign" (simple | enclosed) Field* "@end sign"
    enclosed  = "|" compound "|"
    compound = compoundPart (compoundOperator compoundPart)*
    compoundPart = group | simple | repeated
    compoundOperator = "." | "+" | "@" | "&" | "%" | "×"
    group = "(" compound ")"
    repeated = digit+ "x" simple
    simple = ~digit (upper | subscript | digit)+ modifier?
    modifier = "@" (lower | digit+)

    Field = List
          | Unicode
          | ScopedField
          | FreeTextField<"@lit">
          | Value

    ScopedField = FreeTextField<"@note">
                | FreeTextField<"@inote">

    List = "@list" alnum+

    Unicode = "@ucode" NonemptyListOf<codepoint, ".">
    codepoint = "x" hexDigit+
    
    FreeTextField<tag> = tag freetext
    freetext = (~"\n" any)+

    Value = ("@v?" | "@v-"| "@v") language? unknown? (reading | slashedReading)?  ("[" Instance "]")? ScopedField*
    Instance = (NoBrackets | Brackets)+
    Brackets = "[" NoBrackets "]"
    NoBrackets = (~("[" | "]") any)+
    language = "%" lower+
    slashedReading = "/" reading "/"
    reading = (digit+ ("/" digit+)*)? "("? (lower | subscript | "-" | "ʾ")+ modifier* ")"?
    unknown = "[...]" | "..." | "?"

    subscript =  "ₓ" | "₀".."₉"
    upper += "Š" | "Ṣ" | "Ṭ" | "Ś" | "Ḫ" | "Ŋ"
    lower += "š" | "ṣ" | "ṭ" | "ś" | "ḫ" | "ŋ"
 } 