Asl {
    Sign = "@sign" signName Field* "@end sign"

    signName = simpleSign | complexSign
    simpleSign = singleSign modifier?
    complexSign = "|" multiSign modifier? "|"
    multiSignParen = "(" multiSign ")"
    multiSign = nestedSign ("." nestedSign)*
    nestedSign = singleSign ("×" (singleSign | multiSignParen))*
    singleSign = (upper | subscript)+
    modifier = "@" (lower | digit+)

    Field = List
          | Unicode
          | FreeTextField<"@note">
          | FreeTextField<"@inote">
          | FreeTextField<"@lit">
          | Value

    List = "@list" alnum+

    Unicode = "@ucode" NonemptyListOf<codepoint, ".">
    codepoint = "x" hexDigit+
    
    FreeTextField<tag> = tag freetext
    freetext = (~"\n" any)+

    Value = "@v" language? (reading | slashedReading)
    language = "%" lower+
    slashedReading = "/" reading "/"
    reading = (lower | subscript | "-")+ modifier?

    subscript =  "ₓ" | "₀".."₉"
    upper += "Š" | "Ṣ" | "Ṭ" | "Ś" | "Ḫ" | "Ŋ"
    lower += "š" | "ṣ" | "ṭ" | "ś" | "ḫ" | "ŋ"
 } 