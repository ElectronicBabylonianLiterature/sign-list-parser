Asl {
    Sign = "@sign" (simple | enclosed) Field* "@end sign"
    enclosed  = "|" compound "|"
    compound = compoundPart (compoundOperator compoundPart)*
    compoundPart = group | simple | repeated
    compoundOperator = "." | "+" | "@" | "&" | "%" | "×"
    group = "(" compound ")"
    repeated = digit+ "x" simple
    simple = ~digit (upper | subscript | digit)+ modifier?
    modifier = "@" (lower | digit+)

    Field = List
          | Unicode
          | FreeTextField<"@note">
          | FreeTextField<"@inote">
          | FreeTextField<"@lit">
          | Value

    List = "@list" alnum+

    Unicode = "@ucode" NonemptyListOf<codepoint, ".">
    codepoint = "x" hexDigit+
    
    FreeTextField<tag> = tag freetext
    freetext = (~"\n" any)+

    Value = "@v" language? lacuna? (reading | slashedReading)?  ("[" Instance "]")?
    Instance = (NoBrackets | Brackets)+
    Brackets = "[" NoBrackets "]"
    NoBrackets = (~("[" | "]") any)+
    language = "%" lower+
    slashedReading = "/" reading "/"
    reading = (lower | subscript | "-")+ modifier?
    lacuna = "[...]" | "..."

    subscript =  "ₓ" | "₀".."₉"
    upper += "Š" | "Ṣ" | "Ṭ" | "Ś" | "Ḫ" | "Ŋ"
    lower += "š" | "ṣ" | "ṭ" | "ś" | "ḫ" | "ŋ"
 } 